(ns bru-9.scenes.tbezier
  (:require [bru-9.color.core :as c]
            [bru-9.color.infinite :as ci]
            [bru-9.field.core :as f]
            [bru-9.field.linear :as fl]
            [bru-9.geom.bezier :as b]
            [bru-9.geom.brush :as br]
            [bru-9.util :as u]
            [bru-9.geom.ptf :as ptf]
            [thi.ng.geom.core :as g]
            [thi.ng.geom.vector :as v]
            [thi.ng.math.core :as m]
            [thi.ng.geom.webgl.glmesh :as glm]
            [thi.ng.geom.attribs :as attr]
            [bru-9.color.infinite :as ci]
            [bru-9.interop :as i]
            [thi.ng.color.core :as tc]))

(defn sample-brush [brushfn]
  (let [sample-count 100
        bfn (fn [i] (brushfn (/ i (dec sample-count))))]
    (map bfn (range sample-count))))

(def brushes
  {
   :sine05 (sample-brush #(br/sine % 0.005 m/PI 5 2))
   :sine09 (sample-brush #(br/sine % 0.009 m/PI 5 2))
   :sine11 (sample-brush #(br/sine % 0.011 m/PI 5 2))
   :sine20 (sample-brush #(br/sine % 0.020 m/PI 5 2))

   ;:wobble24 (sample-brush #(br/wobbler % 0.24))

   ;:spiky40 (sample-brush #(br/two-sided-spikes % 0.4 5))
   ;:spiky24 (sample-brush #(br/two-sided-spikes % 0.24 5))
   ;:spiky12 (sample-brush #(br/two-sided-spikes % 0.12 5))
   ;:spiky6 (sample-brush #(br/two-sided-spikes % 0.06 5))

   ;:sine (sample-brush #(br/sine % 0.5 (rand m/TWO_PI) 5 1))
   ;:rotating-quad4 (sample-brush #(br/rotating-quad % 0.4 m/HALF_PI))
   ;:rotating-quad8 (sample-brush #(br/rotating-quad % 0.8 m/HALF_PI))
   ;:noise-quad (sample-brush #(br/noise-quad % 0.4))
   })

(def config {:background-color 0xEEEEEE
             :start-positions-hops 200
             :start-positions-axis-following 2.0
             :start-positions-walk-multiplier 0.03
             :curve-tightness-min 0.04
             :curve-tightness-max 0.1
             :spline-hops 5
             :offset-radius 0.05
             :field-dimensions [5 5 5]
             :field-count 2
             :field-general-direction v/V3X
             :field-random-following 1.0
             :mulfn-base 0.6
             :mulfn-jump-chance 0.2
             :mulfn-jump-intensity 1.2
             :wander-probability 0.5
             :spline-resolution 8
             :mesh-geometry-size 131070
             :brushes (vals brushes)
             :infinite-params {:hue 0.0
                               :saturation 0.0
                               :brightness 0.0}
             :rotation-speed 0.00015
             :camera-distance 11
             :camera-look-at 5})

(defn- mulfn [_]
  (let [{:keys [mulfn-base mulfn-jump-chance mulfn-jump-intensity]} config]
    (+ mulfn-base (if (< (rand) mulfn-jump-chance) mulfn-jump-intensity 0))))

(defn- field-generator [_ random-intensity direction]
  (m/+ direction (v/randvec3 random-intensity)))

(defn- make-directions [initial count]
  (loop [dirs [initial], i count]
    (if (> i 0)
      (recur (conj dirs (m/- (last dirs))) (dec i))
      (rest dirs))))

(defn- make-fields []
  (let [{:keys [field-count
                field-general-direction
                field-random-following
                field-dimensions]} config
        dirs (make-directions field-general-direction field-count)
        fgen (fn [_ dir] (field-generator _ field-random-following dir))
        constructor (fn [dir] (fl/linear-field field-dimensions #(fgen % dir)))]
    (map constructor dirs)))

(defn- make-start-positions-field [direction]
  (let [random-follow (:field-random-following config)]
    (fl/linear-field (:field-dimensions config)
                     #(field-generator % random-follow direction))))

(defn- make-start-positions [field]
  (let [mulfn (fn [_] (:start-positions-walk-multiplier config))]
    (f/walk field v/V3 (:start-positions-hops config) mulfn)))

(def start-positions
  (->> (:start-positions-axis-following config)
       (m/* (:field-general-direction config))
       make-start-positions-field
       make-start-positions))

(defn- make-field-splines
  "Returns a collection of splines generated by walking the given fields."
  [fields]
  (let [{:keys [spline-hops
                offset-radius
                curve-tightness-min
                curve-tightness-max
                wander-probability]} config
        offset-positions (map #(m/+ % (v/randvec3 offset-radius))
                              start-positions)]
    (map #(b/spline-wander fields % (+ 2 (rand-int (- spline-hops 2))) mulfn
                           (u/rand-range curve-tightness-min
                                         curve-tightness-max)
                           wander-probability)
         offset-positions)))

(defn- draw-fields
  "Draws the given field using the given infinite palette."
  [scene fields palette]
  (let [mesh-acc (glm/gl-mesh (:mesh-geometry-size config) #{:col})
        res (:spline-resolution config)
        brushes (:brushes config)
        generate-profiles
        (fn [count brush]
          (let [mfn
                (fn [i]
                  (let [t (/ i (dec count))]
                    (u/nth01 brush t)))]
            (map mfn (range count))))
        ptf-spline
        (fn [acc spline color]
          (let [colors (attr/const-face-attribs (repeat color))
                vertices (g/vertices spline res)
                brush (rand-nth brushes)]
            (ptf/sweep-mesh vertices
                            (generate-profiles (count vertices) brush)
                            {:mesh acc, :attribs {:col colors}})))
        mesh (reduce #(ptf-spline %1 (first %2) (second %2))
                     mesh-acc
                     (map vector (make-field-splines fields) palette))
        three-mesh (i/three-mesh mesh)]
    (.add scene three-mesh)))

(defn- setup-camera [camera pivot-pos]
  (set! (.-x (.-position camera)) (:camera-look-at config))
  (set! (.-y (.-position camera)) 0)
  (set! (.-z (.-position camera)) (:camera-distance config))
  (.lookAt camera pivot-pos))

(defonce camera-pivot (THREE.Object3D.))

(defn setup [initial-context]
  (let [pivot-pos (THREE.Vector3. (:camera-look-at config) 0 0)
        camera (:camera initial-context)
        scene (:scene initial-context)
        palette (c/random-palette)]
    (draw-fields (:scene initial-context)
                 (make-fields)
                 (ci/infinite-palette palette (:infinite-params config)))
    (setup-camera camera pivot-pos)
    (set! (.-position camera-pivot) pivot-pos)
    (.add camera-pivot camera)
    (.add scene camera-pivot)

    (.setClearColor (:renderer initial-context) (:background-color config) 1.0)
    initial-context))

(defn reload [context]
  (let [scene (:scene context)]
    (u/clear-scene scene)
    (.remove camera-pivot (:camera context))
    (setup context)))

(defn animate [context]
  (let [rot (.-x (.-rotation camera-pivot))]
    (set! (.-x (.-rotation camera-pivot)) (+ rot (:rotation-speed config)))
    context))
